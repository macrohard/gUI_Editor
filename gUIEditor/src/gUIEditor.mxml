<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   closing="closeWindowHandler(event)" creationComplete="creationCompleteHandler()"
					   title="gUI界面编辑器">
	<fx:Style source="gUIEditor.css"/>
	<fx:Declarations>
		<fx:XMLList id="menuDefines">
			<menu label="文件">
				<menuitem label="新建界面" data="new"/>
				<menuitem label="打开界面" data="open"/>
				<menuitem label="保存" data="save"/>
				<menuitem label="另存为..." data="saveAs"/>
				<menuitem type="separator"/>
				<menuitem label="新建项目" data="createProject"/>
				<menuitem label="打开项目" data="openProject"/>
				<menuitem type="separator"/>
				<menuitem label="导出界面" data="exportActionScript"/>
				<menuitem label="导出皮肤样式" data="exportSkinStyle"/>
				<menuitem label="导出资源包" data="exportAssets" enabled="false"/>
				<menuitem type="separator"/>
				<menuitem label="退出" data="exit"/>
			</menu>
			<menu label="查看">
				<menuitem label="组件面板" data="component"/>
				<menuitem label="大纲面板" data="tree"/>
				<menuitem label="属性面板" data="properties"/>
				<menuitem type="separator"/>
				<menuitem label="皮肤管理器" data="skins"/>
				<menuitem label="样式管理器" data="styles"/>
				<menuitem label="资源管理器" data="assets" enabled="false"/>
				<menuitem type="separator"/>
				<menuitem label="重置面板布局" data="layout"/>
				<menuitem label="刷新" data="refresh"/>
			</menu>
			<menu label="关于">
				<menuitem label="帮助" data="help" enabled="false"/>
			</menu>
		</fx:XMLList>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.macro.gUI.editor.dialog.CreateGUI;
			import com.macro.gUI.editor.dialog.OpenGUI;
			import com.macro.gUI.editor.dialog.SaveAsGUI;
			import com.macro.gUI.editor.panel.ComponentsPanel;
			import com.macro.gUI.editor.panel.Inspector;
			import com.macro.gUI.editor.panel.TreePanel;
			import com.macro.gUI.editor.project.ProjectManager;
			import com.macro.gUI.editor.project.Workbench;
			import com.macro.gUI.editor.skin.SkinListWindow;
			import com.macro.gUI.editor.style.StyleListWindow;
			
			import mx.collections.*;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.MenuEvent;
			import mx.managers.PopUpManager;
			
			
			
			public var skinEditor:SkinListWindow;
			
			public var styleEditor:StyleListWindow;
			
			public var componentsPanel:ComponentsPanel = new ComponentsPanel();
			
			public var outlinePanel:TreePanel = new TreePanel();
			
			public var inspector:Inspector = new Inspector();
			
			
			
			protected function menuShowHandler(e:MenuEvent):void
			{
				if (e.menuBar != null && e.menuBar.selectedIndex == 0)
				{
					var hasProject:Boolean = ProjectManager.inst.configFile != null;
					var hasDocument:Boolean = ProjectManager.inst.workFile != null;
					
					var xml:XML = menuDefines[0];
					xml.menuitem.(attribute("data") == "new").@enabled = hasProject;
					xml.menuitem.(attribute("data") == "open").@enabled = hasProject;
					xml.menuitem.(attribute("data") == "save").@enabled = hasDocument;
					xml.menuitem.(attribute("data") == "saveAs").@enabled = hasDocument;
					xml.menuitem.(attribute("data") == "exportActionScript").@enabled = hasDocument;
				}
			}
			
			protected function menuItemClickHandler(e:MenuEvent):void
			{
				var key:String = e.item.@data;
				switch (key)
				{
					case "new":
					{
						guiNew();
						break;
					}
					case "open":
					{
						guiOpen();
						break;
					}
					case "save":
					{
						guiSave();
						break;
					}
					case "saveAs":
					{
						guiSaveAs();
						break;
					}
					case "createProject":
					{
						createProject();
						break;
					}
					case "openProject":
					{
						openProject();
						break;
					}
					case "exportActionScript":
					{
						break;
					}
					case "exportSkinStyle":
					{
						break;
					}
					case "exportAssets":
					{
						break;
					}
					case "exit":
					{
						closeWindow();
						break;
					}
					case "component":
					{
						PopUpManager.addPopUp(componentsPanel, this);
						break;
					}
					case "tree":
					{
						PopUpManager.addPopUp(outlinePanel, this);
						break;
					}
					case "properties":
					{
						PopUpManager.addPopUp(inspector, this);
						break;
					}
					case "skins":
					{
						if (skinEditor == null || skinEditor.closed)
						{
							skinEditor = new SkinListWindow();
							skinEditor.open();
						}
						skinEditor.orderToFront();
						break;
					}
					case "styles":
					{
						if (styleEditor == null || styleEditor.closed)
						{
							styleEditor = new StyleListWindow();
							styleEditor.open();
						}
						styleEditor.orderToFront();
						break;
					}
					case "assets":
					{
						break;
					}
					case "layout":
					{
						resetPanelLayout();
						break;
					}
					case "refresh":
					{
						break;
					}
					case "help":
					{
						break;
					}
				}
			}
			
			protected function creationCompleteHandler():void
			{
				this.addElement(ProjectManager.inst.workbench);
				resetPanelLayout();
				
				var appXml:XML = NativeApplication.nativeApplication.applicationDescriptor;
				var ns:Namespace = appXml.namespace();
				menuDefines[2].appendChild(<menuitem label={"Version: " + appXml.ns::versionNumber}/>);
				
				ProjectManager.inst.mainWindow = this;
				ProjectManager.inst.loadAppConfig();
			}
			
			protected function closeWindowHandler(e:Event):void
			{
				e.preventDefault();
				closeWindow();
			}
			
			private function closeWindow():void
			{
				if (checkUnsavedDoc(closeWindow))
					return;
				
				if (ProjectManager.inst.configFile != null)
				{
					ProjectManager.inst.saveConfig();
					ProjectManager.inst.saveAppConfig();
				}
				
				this.exit();
			}
			
			
			private function resetPanelLayout():void
			{
				var t:int = 40;
				var b:int = 40;
				var gap:int = 10;
				var w:int = 300;
				var h:int = 150;
				
				// 总高度 - 顶部留空 - 底部留空 - 间距
				var ph:int = (this.height - t - b - gap) >> 1;
				
				// 组件面板
				componentsPanel.width = w;
				componentsPanel.height = ph;
				componentsPanel.x = this.width - componentsPanel.width - gap;
				componentsPanel.y = t;
				PopUpManager.addPopUp(componentsPanel, this);
				
				// 大纲面板
				outlinePanel.width = w;
				outlinePanel.height = ph;
				outlinePanel.x = componentsPanel.x;
				outlinePanel.y = componentsPanel.y + componentsPanel.height + gap;
				PopUpManager.addPopUp(outlinePanel, this);
				
				// 初始化属性面板
				inspector.width = componentsPanel.x - gap * 2;
				inspector.height = h;
				inspector.x = gap;
				inspector.y = this.height - inspector.height - b;
				PopUpManager.addPopUp(inspector, this);
			}
			
			private function createProject():void
			{
				if (checkUnsavedDoc(createProject))
					return;
				
				var tmp:File = File.documentsDirectory;
				tmp.browseForDirectory("请选择新项目的工作目录");
				tmp.addEventListener(Event.SELECT, function(e:Event):void
				{
					tmp.removeEventListener(Event.SELECT, arguments.callee);
					if (tmp.getDirectoryListing().length > 0)
					{
						Alert.show("目录不为空，在此目录中新建项目将覆盖旧文件，你确定吗？", "警告", Alert.OK | Alert.CANCEL, null, function(e:CloseEvent):void
						{
							if (e.detail == Alert.OK)
							{
								ProjectManager.inst.createProject(tmp);
							}
						});
					}
					else
					{
						ProjectManager.inst.createProject(tmp);
					}
				});
			}
			
			private function openProject():void
			{
				if (checkUnsavedDoc(openProject))
					return;
				
				var tmp:File = File.documentsDirectory;
				tmp.browseForDirectory("请选择项目的工作目录");
				tmp.addEventListener(Event.SELECT, function(e:Event):void
				{
					tmp.removeEventListener(Event.SELECT, arguments.callee);
					if (!ProjectManager.inst.openProject(tmp))
					{
						Alert.show("项目打开失败，将在此目录中创建新项目，你确定吗？", "警告", Alert.YES | Alert.NO, null, function(e:CloseEvent):void
						{
							if (e.detail == Alert.YES)
							{
								ProjectManager.inst.createProject(tmp);
							}
						});
					}
				});
			}
			
			
			private function guiNew():void
			{
				if (checkUnsavedDoc(guiNew))
					return;
				
				var w:CreateGUI = new CreateGUI();
				PopUpManager.addPopUp(w, this, true);
				PopUpManager.centerPopUp(w);
			}
			
			private function guiOpen():void
			{
				if (checkUnsavedDoc(guiOpen))
					return;
				
				var w:OpenGUI = new OpenGUI();
				PopUpManager.addPopUp(w, this, true);
				PopUpManager.centerPopUp(w);
			}
			
			private function guiSave():void
			{
				ProjectManager.inst.saveDoc();
			}
			
			private function guiSaveAs():void
			{
				var w:SaveAsGUI = new SaveAsGUI();
				PopUpManager.addPopUp(w, this, true);
				PopUpManager.centerPopUp(w);
			}
			
			private function checkUnsavedDoc(caller:Function):Boolean
			{
				if (!ProjectManager.inst.workbench.unsaved)
					return false;
				
				Alert.show("当前文档未保存，是否保存？", "提示", Alert.YES | Alert.NO | Alert.CANCEL, null, function(e:CloseEvent):void
				{
					if (e.detail == Alert.YES)
					{
						ProjectManager.inst.saveDoc();
						caller.apply();
					}
					else if (e.detail == Alert.NO)
					{
						caller.apply();
					}
				});
				
				return true;
			}
			
		]]>
	</fx:Script>

	<mx:MenuBar id="topMenuBar" width="100%" height="30" dataProvider="{menuDefines}"
				itemClick="menuItemClickHandler(event)" labelField="@label"
				menuShow="menuShowHandler(event)"/>

</s:WindowedApplication>
