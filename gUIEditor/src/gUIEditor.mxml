<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   closing="closeWindowHandler()"
					   creationComplete="creationCompleteHandler()">
	<fx:Style source="gUIEditor.css"/>
	<fx:Declarations>
		<fx:XMLList id="menuDefines">
			<menu label="文件">
				<menuitem label="新建界面" data="new"/>
				<menuitem label="打开界面" data="open"/>
				<menuitem label="保存" data="save"/>
				<menuitem label="另存为..." data="saveAs"/>
				<menuitem type="separator"/>
				<menuitem label="新建项目" data="createProject"/>
				<menuitem label="打开项目" data="openProject"/>
				<menuitem type="separator"/>
				<menuitem label="导出界面" data="exportActionScript"/>
				<menuitem label="导出皮肤样式" data="exportSkinStyle"/>
				<menuitem label="导出资源包" data="exportAssets" enabled="false"/>
				<menuitem type="separator"/>
				<menuitem label="退出" data="exit"/>
			</menu>
			<menu label="查看">
				<menuitem label="组件面板" data="component"/>
				<menuitem label="大纲面板" data="tree"/>
				<menuitem label="属性面板" data="properties"/>
				<menuitem type="separator"/>
				<menuitem label="皮肤管理器" data="skins"/>
				<menuitem label="样式管理器" data="styles"/>
				<menuitem label="资源管理器" data="assets" enabled="false"/>
				<menuitem type="separator"/>
				<menuitem label="重置面板布局" data="layout"/>
				<menuitem label="刷新" data="refresh"/>
			</menu>
			<menu label="关于">
				<menuitem label="帮助" data="help" enabled="false"/>
			</menu>
		</fx:XMLList>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.macro.gUI.editor.dialog.CreateGUI;
			import com.macro.gUI.editor.dialog.OpenGUI;
			import com.macro.gUI.editor.dialog.SaveAsGUI;
			import com.macro.gUI.editor.panel.ComponentsPanel;
			import com.macro.gUI.editor.panel.Inspector;
			import com.macro.gUI.editor.panel.TreePanel;
			import com.macro.gUI.editor.project.ProjectManager;
			import com.macro.gUI.editor.project.Workbench;
			import com.macro.gUI.editor.skin.SkinListWindow;
			import com.macro.gUI.editor.style.StyleListWindow;
			
			import mx.collections.*;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.MenuEvent;
			import mx.managers.PopUpManager;
			
			
			
			private var _skinEditor:SkinListWindow;
			
			private var _styleEditor:StyleListWindow;
			
			private var _componentsPanel:ComponentsPanel = new ComponentsPanel();
			
			private var _treePanel:TreePanel = new TreePanel();
			
			private var _inspector:Inspector = new Inspector();
			
			
			protected function menuShowHandler(e:MenuEvent):void
			{
				if (e.menuBar != null && e.menuBar.selectedIndex == 0)
				{
					var hasProject:Boolean = ProjectManager.inst.folder != null;
					var hasDocument:Boolean = ProjectManager.inst.workbench.doc != null;
					
					var xml:XML = menuDefines[0];
					xml.menuitem.(attribute("data") == "new").@enabled = hasProject;
					xml.menuitem.(attribute("data") == "open").@enabled = hasProject;
					xml.menuitem.(attribute("data") == "save").@enabled = hasDocument;
					xml.menuitem.(attribute("data") == "saveAs").@enabled = hasDocument;
					xml.menuitem.(attribute("data") == "exportActionScript").@enabled = hasDocument;
				}
			}
			
			protected function menuItemClickHandler(e:MenuEvent):void
			{
				var key:String = e.item.@data;
				switch (key)
				{
					case "new":
					{
						guiNew();
						break;
					}
					case "open":
					{
						guiOpen();
						break;
					}
					case "save":
					{
						guiSave();
						break;
					}
					case "saveAs":
					{
						guiSaveAs();
						break;
					}
					case "createProject":
					{
						createProject();
						break;
					}
					case "openProject":
					{
						openProject();
						break;
					}
					case "exportActionScript":
					{
						break;
					}
					case "exportSkinStyle":
					{
						break;
					}
					case "exportAssets":
					{
						break;
					}
					case "exit":
					{
						closeWindowHandler();
						this.exit();
						break;
					}
					case "component":
					{
						PopUpManager.addPopUp(_componentsPanel, this);
						break;
					}
					case "tree":
					{
						PopUpManager.addPopUp(_treePanel, this);
						break;
					}
					case "properties":
					{
						PopUpManager.addPopUp(_inspector, this);
						break;
					}
					case "skins":
					{
						if (_skinEditor == null || _skinEditor.closed)
						{
							_skinEditor = new SkinListWindow();
							_skinEditor.open();
						}
						_skinEditor.orderToFront();
						break;
					}
					case "styles":
					{
						if (_styleEditor == null || _styleEditor.closed)
						{
							_styleEditor = new StyleListWindow();
							_styleEditor.open();
						}
						_styleEditor.orderToFront();
						break;
					}
					case "assets":
					{
						break;
					}
					case "layout":
					{
						resetPanelLayout();
						break;
					}
					case "refresh":
					{
						break;
					}
					case "help":
					{
						break;
					}
				}
			}
			
			protected function creationCompleteHandler():void
			{
				this.addElement(ProjectManager.inst.workbench);
				resetPanelLayout();
				
				var appXml:XML = NativeApplication.nativeApplication.applicationDescriptor;
				var ns:Namespace = appXml.namespace();
				menuDefines[2].appendChild(<menuitem label={"Version: " + appXml.ns::versionNumber}/>);
				
				ProjectManager.inst.loadAppConfig();
				setStatusText();
			}
			
			protected function closeWindowHandler():void
			{
				if (ProjectManager.inst.folder != null)
				{
					ProjectManager.inst.saveConfig();
					ProjectManager.inst.saveAppConfig();
				}
			}
			
			
			private function resetPanelLayout():void
			{
				var t:int = 40;
				var b:int = 40;
				var gap:int = 10;
				var w:int = 300;
				var h:int = 150;
				
				// 总高度 - 顶部留空 - 底部留空 - 间距
				var ph:int = (this.height - t - b - gap) >> 1;
				
				// 组件面板
				_componentsPanel.width = w;
				_componentsPanel.height = ph;
				_componentsPanel.x = this.width - _componentsPanel.width - gap;
				_componentsPanel.y = t;
				PopUpManager.addPopUp(_componentsPanel, this);
				
				// 大纲面板
				_treePanel.width = w;
				_treePanel.height = ph;
				_treePanel.x = _componentsPanel.x;
				_treePanel.y = _componentsPanel.y + _componentsPanel.height + gap;
				PopUpManager.addPopUp(_treePanel, this);
				
				// 初始化属性面板
				_inspector.width = _componentsPanel.x - gap * 2;
				_inspector.height = h;
				_inspector.x = gap;
				_inspector.y = this.height - _inspector.height - b;
				PopUpManager.addPopUp(_inspector, this);
			}
			
			private function createProject():void
			{
				var tmp:File = File.documentsDirectory;
				tmp.browseForDirectory("请选择新项目的工作目录");
				tmp.addEventListener(Event.SELECT, function(e:Event):void
				{
					tmp.removeEventListener(Event.SELECT, arguments.callee);
					if (tmp.getDirectoryListing().length > 0)
					{
						Alert.show("目录不为空，在此目录中新建项目将覆盖旧文件，你确定吗？", "警告", Alert.OK | Alert.CANCEL, null, function(e:CloseEvent):void
						{
							if (e.detail == Alert.OK)
							{
								ProjectManager.inst.createProject(tmp);
								setStatusText();
							}
						});
					}
					else
					{
						ProjectManager.inst.createProject(tmp);
						setStatusText();
					}
				});
			}
			
			private function openProject():void
			{
				var tmp:File = File.documentsDirectory;
				tmp.browseForDirectory("请选择项目的工作目录");
				tmp.addEventListener(Event.SELECT, function(e:Event):void
				{
					tmp.removeEventListener(Event.SELECT, arguments.callee);
					if (ProjectManager.inst.openProject(tmp))
					{
						setStatusText();
					}
					else
					{
						Alert.show("项目打开失败，将在此目录中创建新项目，你确定吗？", "警告", Alert.YES | Alert.NO, null, function(e:CloseEvent):void
						{
							if (e.detail == Alert.YES)
							{
								ProjectManager.inst.createProject(tmp);
								setStatusText();
							}
						});
					}
				});
			}
			
			private function setStatusText():void
			{
				var url:String = ProjectManager.inst.folder;
				this.statusText.text = (url == null ? "没有打开的项目" : "工作目录：" + url);
			}
			
			
			private function guiNew():void
			{
				var w:CreateGUI = new CreateGUI();
				PopUpManager.addPopUp(w, this, true);
				PopUpManager.centerPopUp(w);
			}
			
			private function guiOpen():void
			{
				var w:OpenGUI = new OpenGUI();
				PopUpManager.addPopUp(w, this, true);
				PopUpManager.centerPopUp(w);
			}
			
			private function guiSave():void
			{
				ProjectManager.inst.saveInterface();
			}
			
			private function guiSaveAs():void
			{
				var w:SaveAsGUI = new SaveAsGUI();
				PopUpManager.addPopUp(w, this, true);
				PopUpManager.centerPopUp(w);
			}
			
		]]>
	</fx:Script>

	<mx:MenuBar id="topMenuBar" width="100%" height="30" dataProvider="{menuDefines}"
				itemClick="menuItemClickHandler(event)" menuShow="menuShowHandler(event)" labelField="@label"/>

</s:WindowedApplication>
