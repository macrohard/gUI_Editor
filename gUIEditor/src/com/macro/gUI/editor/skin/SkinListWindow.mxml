<?xml version="1.0" encoding="utf-8"?>
<s:Window xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:s="library://ns.adobe.com/flex/spark"
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  width="1000" height="700" creationComplete="creationCompleteHandler()"
		  showStatusBar="false" title="皮肤列表">
	<fx:Script>
		<![CDATA[
			import com.macro.gUI.GameUI;
			import com.macro.gUI.editor.project.ProjectManager;
			import com.macro.gUI.editor.project.SkinConfig;
			import com.macro.gUI.skin.SkinDef;
			import com.macro.utils.StrUtil;
			
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import spark.events.GridEvent;
			
			[Bindable]
			private var skins:ArrayList;


			protected function creationCompleteHandler():void
			{
				skins = new ArrayList();
				dataGrid.dataProvider = skins;
				
				var config:SkinConfig = ProjectManager.inst.skinConfig;
				for each (var item:XML in config.configXML.item)
				{
					var data:Object = new Object();
					data.key = item.@id.toString();
					data.rect = StrUtil.format("[x:{0}, y:{1}, width:{2}, height:{3}]", [item.@x, item.@y, item.@width, item.@height]);
					data.align = "0x" + int(item.@align).toString(16);
					data.image = config.getDisplayObject(item.@id);
					skins.addItem(data);
				}
			}
			
			protected function itemDoubleClickHandler(e:GridEvent):void
			{
				if (e.rowIndex != -1)
				{
					var w:SkinEditor = new SkinEditor();
					var item:XML = ProjectManager.inst.skinConfig.getDefine(skins.getItemAt(e.rowIndex).key);
					w.init(this, e.rowIndex, item);
					PopUpManager.addPopUp(w, this, true);
					PopUpManager.centerPopUp(w);
				}
			}
			
			public function update(index:int, item:XML, source:DisplayObject):void
			{
				var data:Object = skins.getItemAt(index);
				data.image = source;
				data.rect = StrUtil.format("[x:{0}, y:{1}, width:{2}, height:{3}]", [item.@x, item.@y, item.@width, item.@height]);;
				data.align = "0x" + int(item.@align).toString(16);
				skins.setItemAt(data, index);
				
				ProjectManager.inst.skinConfig.setDisplayObject(item.@id, source);
				GameUI.skinManager.createSkin(SkinDef[item.@id], source, new Rectangle(item.@x, item.@y, item.@width, item.@height), item.@align);
			}
			
		]]>
	</fx:Script>
	<s:DataGrid id="dataGrid" x="0" y="0" width="100%" height="100%" doubleClickEnabled="true"
				gridDoubleClick="itemDoubleClickHandler(event)" rowHeight="50">
		<s:columns>
			<s:ArrayList>
				<s:GridColumn dataField="key" headerText="皮肤常量"/>
				<s:GridColumn dataField="rect" headerText="九切参数"/>
				<s:GridColumn dataField="align" headerText="对齐方式"/>
				<s:GridColumn headerText="预览" itemRenderer="com.macro.gUI.editor.skin.PreviewCell"/>
			</s:ArrayList>
		</s:columns>
	</s:DataGrid>
</s:Window>
