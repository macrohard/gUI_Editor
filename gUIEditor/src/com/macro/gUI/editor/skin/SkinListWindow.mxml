<?xml version="1.0" encoding="utf-8"?>
<s:Window xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:s="library://ns.adobe.com/flex/spark"
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  width="1000" height="700" creationComplete="creationCompleteHandler()"
		  showStatusBar="false" title="皮肤列表">
	<fx:Script>
		<![CDATA[
			import com.macro.gUI.editor.project.ProjectManager;
			import com.macro.gUI.editor.project.SkinConfiguration;
			
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import spark.events.GridEvent;
			
			[Bindable]
			private var skins:ArrayList;


			protected function creationCompleteHandler():void
			{
				skins = new ArrayList();
				dataGrid.dataProvider = skins;
				
				var config:SkinConfiguration = ProjectManager.inst.skinConfig;
				for each (var xml:XML in config.configXML.item)
				{
					var data:Object = new Object();
					data.key = xml.@id.toString();
					data.rect = new Rectangle(xml.@x, xml.@y, xml.@width, xml.@height);
					data.align = int(xml.@align);
					data.image = config.getDisplayObject(xml.@id);
					skins.addItem(data);
				}
			}
			
			protected function itemDoubleClickHandler(e:GridEvent):void
			{
				if (e.rowIndex != -1)
				{
					var w:SkinEditor = new SkinEditor();
					w.init(this, e.rowIndex, skins.getItemAt(e.rowIndex));
					PopUpManager.addPopUp(w, this, true);
					PopUpManager.centerPopUp(w);
				}
			}
			
			public function update(index:int, source:DisplayObject, grid:Rectangle, align:int, filename:String = null):void
			{
				var data:Object = skins.getItemAt(index);
				data.image = source;
				data.rect = grid;
				data.align = align;
				skins.setItemAt(data, index);
				
				ProjectManager.inst.skinConfig.setDefine(data.key, source, grid, align, filename);
			}
			
		]]>
	</fx:Script>
	<s:DataGrid id="dataGrid" x="0" y="0" width="100%" height="100%" doubleClickEnabled="true"
				gridDoubleClick="itemDoubleClickHandler(event)" variableRowHeight="true">
		<s:columns>
			<s:ArrayList>
				<s:GridColumn dataField="key" headerText="皮肤常量"/>
				<s:GridColumn dataField="rect" headerText="九切参数"/>
				<s:GridColumn dataField="align" headerText="对齐方式"/>
				<s:GridColumn headerText="预览" itemRenderer="com.macro.gUI.editor.skin.PreviewCell"/>
			</s:ArrayList>
		</s:columns>
	</s:DataGrid>
</s:Window>
